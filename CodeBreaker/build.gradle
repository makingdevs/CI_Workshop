buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE")
  }
}

apply plugin: 'groovy'
apply plugin: 'spring-boot'

jar {
  baseName = 'codebreaker'
  version =  '0.1.0'
}

repositories {
  mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
  cucumberRuntime {
    extendsFrom testRuntime
  }
}

task cucumber() {
  dependsOn assemble
  doLast {
    javaexec {
      main = "cucumber.api.cli.Main"
      classpath = configurations.cucumberRuntime
      args = ['--plugin', 'pretty', '--glue', 'src/test/groovy', 'src/test/resources']

    }
  }
}

//Ensure the cucumber tests are executed as part of the build. Makes for a very pretty output.
build.dependsOn cucumber

dependencies {
  cucumberRuntime files("${jar.archivePath}")
  compile 'org.codehaus.groovy:groovy-all:2.4.3'
  compile("org.springframework.boot:spring-boot-starter-thymeleaf")
  testCompile("junit:junit")
  testCompile 'org.seleniumhq.selenium:selenium-firefox-driver:2.46.0'
  testCompile 'org.seleniumhq.selenium:selenium-support:2.46.0'
  testCompile 'info.cukes:cucumber-groovy:1.2.4'
  testCompile 'info.cukes:cucumber-junit:1.2.4'
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.4'
}
