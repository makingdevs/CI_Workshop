plugins {
  id 'org.springframework.boot' version '2.1.5.RELEASE'
  id 'java'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'groovy'

jar {
  baseName = 'codebreaker'
  version =  '0.1.0'
}

repositories {
  mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
  cucumberRuntime {
    extendsFrom testRuntime
  }
}

task cucumber() {
  dependsOn assemble, compileTestJava
  doLast {
    javaexec {
      main = "cucumber.api.cli.Main"
      classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
      args = ['--plugin', 'pretty', '--glue', 'gradle.cucumber', 'src/test/resources']
    }
  }
}

//Ensure the cucumber tests are executed as part of the build. Makes for a very pretty output.
build.dependsOn cucumber

dependencies {
  cucumberRuntime files("${jar.archivePath}")
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  testCompile 'org.springframework.boot:spring-boot-starter-test'
  compile 'org.codehaus.groovy:groovy-all:2.5.7'
  testCompile "junit:junit"
  testCompile 'org.seleniumhq.selenium:selenium-firefox-driver:3.141.59'
  testCompile 'org.seleniumhq.selenium:selenium-support:3.141.59'
  testCompile 'io.cucumber:cucumber-groovy:4.3.1'
  testCompile 'io.cucumber:cucumber-junit:4.3.1'
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.10.2'
}
